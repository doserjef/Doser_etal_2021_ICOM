
model {

  # Priors ----------------------------------------------------------------
  # Initial occupancy
  int.psi.0.mean ~ dunif(0, 1)
  beta.psi.0.mean <- logit(int.psi.0.mean)
  beta.psi.1.mean ~ dnorm(0, 0.1)
  # Persistence
  int.phi.0.mean ~ dunif(0, 1)
  beta.phi.0.mean <- logit(int.phi.0.mean)
  beta.phi.1.mean ~ dnorm(0, 0.1)
  # Colonization
  int.gamma.0.mean ~ dunif(0, 1)
  beta.gamma.0.mean <- logit(int.gamma.0.mean) 
  beta.gamma.1.mean ~ dnorm(0, 0.1)
  # eBird detection
  int.alpha.eb.0.mean ~ dunif(0, 1)
  alpha.eb.0.mean <- logit(int.alpha.eb.0.mean) 
  alpha.eb.1.mean ~ dnorm(0, 0.1)
  # HB data detection
  int.alpha.hb.0.mean ~ dunif(0, 1)
  alpha.hb.0.mean <- logit(int.alpha.hb.0.mean) 
  alpha.hb.1.mean ~ dnorm(0, 0.1)
  # NEON data detection
  int.alpha.neon.0.mean ~ dunif(0, 1)
  alpha.neon.0.mean <- logit(int.alpha.neon.0.mean) 
  alpha.neon.1.mean ~ dnorm(0, 0.1)
  tau.beta.psi.0 ~ dgamma(0.1, 0.1)
  tau.beta.psi.1 ~ dgamma(0.1, 0.1)
  tau.beta.phi.0 ~ dgamma(0.1, 0.1)
  tau.beta.phi.1 ~ dgamma(0.1, 0.1)
  tau.beta.gamma.0 ~ dgamma(0.1, 0.1)
  tau.beta.gamma.1 ~ dgamma(0.1, 0.1)
  tau.alpha.eb.0 ~ dgamma(0.1, 0.1)
  tau.alpha.eb.1 ~ dgamma(0.1, 0.1)
  tau.alpha.hb.0 ~ dgamma(0.1, 0.1)
  tau.alpha.hb.1 ~ dgamma(0.1, 0.1)
  tau.alpha.neon.0 ~ dgamma(0.1, 0.1)
  tau.alpha.neon.1 ~ dgamma(0.1, 0.1)

  # Species-specific coefficients -----------------------------------------
  for (k in 1:K) {
    beta.psi.0[k] ~ dnorm(beta.psi.0.mean, tau.beta.psi.0)
    beta.psi.1[k] ~ dnorm(beta.psi.1.mean, tau.beta.psi.1)
    beta.phi.0[k] ~ dnorm(beta.phi.0.mean, tau.beta.phi.0)
    beta.phi.1[k] ~ dnorm(beta.phi.1.mean, tau.beta.phi.1)
    beta.gamma.0[k] ~ dnorm(beta.gamma.0.mean, tau.beta.gamma.0)
    beta.gamma.1[k] ~ dnorm(beta.gamma.1.mean, tau.beta.gamma.1)
    alpha.eb.0[k] ~ dnorm(alpha.eb.0.mean, tau.alpha.eb.0)
    alpha.eb.1[k] ~ dnorm(alpha.eb.1.mean, tau.alpha.eb.1)
    alpha.hb.0[k] ~ dnorm(alpha.hb.0.mean, tau.alpha.hb.0)
    alpha.hb.1[k] ~ dnorm(alpha.hb.1.mean, tau.alpha.hb.1)
    alpha.neon.0[k] ~ dnorm(alpha.neon.0.mean, tau.alpha.neon.0)
    alpha.neon.1[k] ~ dnorm(alpha.neon.1.mean, tau.alpha.neon.1)
  }

  # Process models --------------------------------------------------------
  for (k in 1:K) {
    # Initial occupancy process -------
    for (i in 1:R) {
        logit(psi.0[i, k]) <- beta.psi.0[k] + beta.psi.1[k] * X.psi[i, 2]
        z[i, k, 1] ~ dbern(psi.0[i, k])
    } # i 

    for (t in 1:(n.years - 1)) {
      # Persistence and colonization --
      for (i in 1:R) {
        logit(phi[i, k, t]) <- beta.phi.0[k] + beta.phi.1[k] * X.phi[i, t, 2]
        logit(gamma[i, k, t]) <- beta.gamma.0[k] + 
                                 beta.gamma.1[k] * X.gamma[i, t, 2]
        z[i, k, t + 1] ~ dbern(z[i, k, t] * phi[i, k, t] + (1 - z[i, k, t]) * gamma[i, k, t])
      } # i
    } # t
    
    for (t in 1:n.years) {
      # Likelihoods -------------------------------------------------------
      # HB Data -----------------------
      for (i in 1:R.hb) {
        for (j in 1:J.hb) {
          logit(p.hb[i, j, k, t]) <- alpha.hb.0[k] + alpha.hb.1[k] * X.hb[i, j, t, 2]
          C[i, j, k, t] ~ dbern(p.hb[i, j, k, t] * z[pixel.hb[i], k, t])
        } # j 
      } # i 

      # eBird Data --------------------
      for (l in 1:R.eb) {
        # Change of support for eBird - 
        z.eb[l, k, t] <- ifelse(sum(z[low[l]:high[l], k, t]) > 0, 1, 0)
        for (j in 1:J.eb) {
          logit(p.eb[j, l, k, t]) <- alpha.eb.0[k] + alpha.eb.1[k] * X.eb[l, j, t, 2]
          y[j, l, k, t] ~ dbern(z.eb[l, k, t] * p.eb[j, l, k, t])
        } # j
      } # l
     
      # NEON Data ---------------------
      for (i in 1:R.neon) {
        for (j in 1:J.neon) {
          logit(p.neon[i, j, k, t]) <- alpha.neon.0[k] + 
                                       alpha.neon.1[k] * X.neon[i, j, t, 2]
          x[i, j, k, t] ~ dbern(p.neon[i, j, k, t] * z[pixel.neon[i], k, t])
        } # j
      } # i
    } # t
  } # k
}
